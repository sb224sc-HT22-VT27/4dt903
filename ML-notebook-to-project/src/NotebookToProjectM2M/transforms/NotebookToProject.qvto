modeltype NotebookMM uses "http://www.g7.org/notebookMM";
modeltype ProjectStuctureMM uses "http://www.g7.org/projectStructureMM";

transformation NotebookToProject(in srcModel : NotebookMM, out targetModel : ProjectStuctureMM);

// property NAME : TYPE = val; Consts
property MAIN_FILE_NAME : String = "main.py";
property DIR_NAMES : Sequence(String) = Sequence{"data", "src", "models", "outputs"};
property REQUIREMENTS_FILE_NAME : String = "requirements.txt";

main() {
	// [MetaModel::Attribute]
	// log(srcModel.rootObjects()[NotebookMM::NotebookModel]->asOrderedSet()->first().toString());
	srcModel.rootObjects()[NotebookMM::NotebookModel] -> map toProjectStructure();
		
	log("Notebook transformed to Project!");
	
}

mapping NotebookMM::NotebookModel::toProjectStructure() : ProjectStuctureMM::ProjectStructure {
	result.name := self.name;
	
	result.filesystemelement := DIR_NAMES->collect(dirName | self.map toDirectory(dirName))->union(Sequence{self.map toRequirementsFile()});
}

mapping NotebookMM::NotebookModel::toDirectory(dirName : String) : ProjectStuctureMM::Directory {
    result.name := dirName;
    result.filesystemelement := if dirName = 'src' then OrderedSet{self.map toMainFile()} else OrderedSet{self.map toKeepFile()} endif;
}

mapping NotebookMM::NotebookModel::toMainFile() : ProjectStuctureMM::File {
    result.name := MAIN_FILE_NAME;
    result.content := generateMainPyContent(self);
}

mapping NotebookMM::NotebookModel::toRequirementsFile() : ProjectStuctureMM::File {
	result.name := REQUIREMENTS_FILE_NAME;
	result.content := extractPackageNames(self);
}

mapping NotebookMM::NotebookModel::toKeepFile() : ProjectStuctureMM::File {
	result.name := ".keep";
	result.content := OrderedSet{};
}

helper generateMainPyContent(notebook : NotebookMM::NotebookModel) : Sequence(String) {
    var contentLines : Sequence(String) := Sequence{};

    // Iterate through all cells in order
    notebook.cells->forEach(cell) {
        if cell.oclIsTypeOf(NotebookMM::MarkdownCell) then {
            // Convert markdown cell to Python docstring
            var mdCell := cell.oclAsType(NotebookMM::MarkdownCell);
            if mdCell.content <> null and mdCell.content <> '' then {
                contentLines += '"""\n';
                contentLines += mdCell.content + '\n';
                contentLines += '"""\n\n';
            } endif;
        } else if cell.oclIsTypeOf(NotebookMM::CodeCell) then {
            // Add code cell source as-is
            var codeCell := cell.oclAsType(NotebookMM::CodeCell);
            if codeCell.source <> null and codeCell.source <> '' then {
                contentLines += codeCell.source + '\n\n';
            } endif;
        } endif endif;
    };

    return contentLines;
}

helper extractPackageNames(notebook : NotebookMM::NotebookModel) : OrderedSet(String) {
	var imports : OrderedSet(String) := notebook.getAllImports();
	var packages : OrderedSet(String) := 
		imports 
		->collect(importStmt | mapImportToPackage(importStmt)) 
		->select(pkg | pkg <> '') 
		->asOrderedSet(); 
	
	return packages;
}

helper mapImportToPackage(importStmt : String) : String {
	if importStmt.indexOf('sklearn') >= 0 then
	return 'scikit-learn'
	elif importStmt.indexOf('cv2') >= 0 then
	return 'opencv-python'
	elif importStmt.indexOf('PIL') >= 0 then
	return 'Pillow'
	elif importStmt.indexOf('pandas') >= 0 then
	return 'pandas'
	elif importStmt.indexOf('numpy') >= 0 then
	return 'numpy'
	elif importStmt.indexOf('matplotlib') >= 0 then
	return 'matplotlib'
	elif importStmt.indexOf('scipy') >= 0 then
	return 'scipy'
	elif importStmt.indexOf('seaborn') >= 0 then
	return 'seaborn'
	elif importStmt.indexOf('joblib') >= 0 then
	return 'joblib'
	elif importStmt.indexOf('tensorflow') >= 0 then
	return 'tensorflow'
	elif importStmt.indexOf('torch') >= 0 then
	return 'torch'
	elif importStmt.indexOf('keras') >= 0 then
	return 'keras'
	elif importStmt.indexOf('flask') >= 0 then
	return 'flask'
	elif importStmt.indexOf('django') >= 0 then
	return 'django'
	elif importStmt.indexOf('requests') >= 0 then
	return 'requests'
	elif importStmt.indexOf('sqlalchemy') >= 0 then
	return 'sqlalchemy'
	elif importStmt.indexOf('pytest') >= 0 then
	return 'pytest'
	elif importStmt.indexOf('yaml') >= 0 then
	return 'PyYAML'
	elif importStmt.indexOf('bs4') >= 0 then
	return 'beautifulsoup4'
	elif importStmt.indexOf('dateutil') >= 0 then
	return 'python-dateutil'
	else
	return ''
	endif
	
	
}